package charleywong.browser;

import js.html.ScrollLogicalPosition;
import react.*;
import react.ReactMacro.jsx;
import js.npm.material_ui.MaterialUi;
import js.npm.react_facebook.ReactFacebook;
import js.npm.react_instagram_embed.InstagramEmbed;
import js.Browser.*;
import haxe.DynamicAccess;
using StringTools;
using Lambda;

@:jsRequire("react-masonry-css", "default")
extern class Masonry extends ReactComponent {}

enum abstract MooncakeType(String) {
    var AnyMooncake = "ä»»ä½•æ¬¾å¼";
    var LotusSeedPaste = "è“®è“‰";
    var EggCustard = "å¥¶é»ƒ";
    var SnowSkin = "å†°çš®/é›ªç³•";
    var Nuts = "äº”ä»/èŠ±ç”Ÿ/æ¦›å­/æœä»";
    var Ham = "é‡‘è¯ç«è…¿";
    var PurpleYam = "ç´«è–¯";
    var Taro = "èŠ‹é ­";
    var Matcha = "æŠ¹èŒ¶/ä¼¯çˆµèŒ¶";
    var Beans = "ç´…è±†/ç¶ è±†/è±†æ²™/è±†è“‰";
    var Mocha = "æœ±å¤åŠ›å’–å•¡";
    var Pineapple = "é³³æ¢¨";
    var Coriander = "é¦™èœ(èŠ«è½)";
    var Vegan = "ç´”ç´ ";
    var PetFriendly = "é©åˆå¯µç‰©é£Ÿç”¨";
}

enum abstract OfferType(String) {
    var Manufactured = "è£½æˆå“";
    var Workshop = "å·¥ä½œåŠ";
    var MaterialAndTools = "ææ–™å·¥å…·";
}

class Mooncake2020 extends ReactComponent {
    var data(get, null):Array<Mooncake2020Data>;
    function get_data() return props.data;

    var mooncakeType(get, set):MooncakeType;
    function get_mooncakeType() return state.mooncakeType;
    function set_mooncakeType(v) {
        setState({
            mooncakeType: v,
        });
        return v;
    }

    var offerType(get, set):OfferType;
    function get_offerType() return state.offerType;
    function set_offerType(v) {
        setState({
            offerType: v,
        });
        return v;
    }

    var resized(get, set):Bool;
    function get_resized() return state.resized;
    function set_resized(v) {
        setState({
            resized: v,
        });
        return v;
    }

    var lastWindowWidth:Float = window.innerWidth;

    static function isMooncakeType(type:MooncakeType, mooncakeName:String):Bool {
        return switch (type) {
            case AnyMooncake:
                true;
            case LotusSeedPaste:
                mooncakeName.contains("è“®è“‰");
            case EggCustard:
                mooncakeName.contains("å¥¶é»ƒ") || mooncakeName.contains("å¥¶çš‡");
            case SnowSkin:
                mooncakeName.contains("å†°çš®") || mooncakeName.contains("é›ªç³•") || mooncakeName.contains("å‡");
            case Nuts:
                mooncakeName.contains("äº”ä»") || mooncakeName.contains("ä¼ä»") || mooncakeName.contains("èŠ±ç”Ÿ") || mooncakeName.contains("æ¦›å­") || mooncakeName.contains("æœä»");
            case Ham:
                mooncakeName.contains("é‡‘è¯ç«è…¿");
            case PurpleYam:
                mooncakeName.contains("ç´«è–¯");
            case Taro:
                mooncakeName.contains("èŠ‹");
            case Matcha:
                mooncakeName.contains("æŠ¹èŒ¶") || mooncakeName.contains("ä¼¯çˆµèŒ¶");
            case Beans:
                mooncakeName.contains("ç´…è±†") || mooncakeName.contains("ç¶ è±†") || mooncakeName.contains("è±†æ²™") || mooncakeName.contains("è±†è“‰");
            case Mocha:
                mooncakeName.contains("æœ±å¤åŠ›å’–å•¡") || mooncakeName.contains("æ‘©å¡å’–å•¡");
            case Pineapple:
                mooncakeName.contains("é³³æ¢¨");
            case Coriander:
                mooncakeName.contains("é¦™èœ");
            case Vegan:
                mooncakeName.contains("ç´”ç´ ");
            case PetFriendly:
                mooncakeName.contains("å¯µç‰©");
        }
    }

    function new(props) {
        super(props);
        state = {
            mooncakeType: AnyMooncake,
            offerType: Manufactured,
            resized: false,
        };
    }

    function onResized() {
        var newWidth = window.innerWidth;
        if (lastWindowWidth != newWidth) {
            resized = true;
            lastWindowWidth = newWidth;
        }
    }

    override function componentDidMount() {
        lastWindowWidth = window.innerWidth;
        window.addEventListener('resize', onResized);
    }

    override function componentWillUnmount() {
        window.removeEventListener('resize', onResized);
    }

    function renderMooncakeName(mooncakeName:String) {
        var classes = ["mooncake-name", "mr-2", "text-nowrap"];
        if (mooncakeType != AnyMooncake && isMooncakeType(mooncakeType, mooncakeName)) {
            classes.push("selected-mooncake-type");
        }
        return jsx('
            <Fragment key=${mooncakeName}>
                <span className=${classes.join(" ")}><span className="mooncake-icon"></span> ${mooncakeName}</span>
                <span> </span>
            </Fragment>
        ');
    }

    function renderMooncake2020Data(d:Mooncake2020Data) {
        var oldInfoWarning = if (!d.current) {
            jsx('<div className="old-info-warning"><i className="fas fa-exclamation-circle"></i> ä»¥ä¸‹ç‚ºå¾€å¹´è³‡è¨Š</div>');
        } else {
            null;
        }
        return jsx('
            <div key=${d.name} className="card">
                <div className="card-body">
                    <h5 className="card-title">${d.name}</h5>
                    <h6 className="card-subtitle mb-2 text-muted">${d.note}</h6>
                    ${oldInfoWarning}
                    <p className="card-text">
                        ${d.types.map(renderMooncakeName)}
                    </p>
                    <div className="mooncake-info mb-2">
                        ${d.info.map(renderInfo)}
                    </div>
                    <a className="card-link badge badge-pill badge-light" target="_blank" href=${d.info[0]}>ğŸ”— è³‡æ–™ä¾†æº</a>
                    <a className="card-link badge badge-pill badge-light" target="_blank" href=${d.charleywong}>ğŸ§ğŸ” åº—èˆ–è¡¨æ…‹FC</a>
                </div>
            </div>
        ');
    }

    function renderInfo(url:String) {
        if (resized) {
            return null;
        }
        if (url.startsWith("https://www.facebook.com/")) {
            return jsx('
                <EmbeddedPost key=${url} href=${url} />
            ');
        } else if (url.startsWith("https://www.instagram.com/")) {
            return jsx('
                <InstagramEmbed key=${url} url=${url} />
            ');
        } else {
            return jsx('
                <div key=${url}>
                    <a target="_blank" href=${url}>${url}</a>
                </div>
            ');
        }
    }

    function onFilterChange() {
        document.querySelector("div.mooncake2020").scrollIntoView({
            block: ScrollLogicalPosition.START,
        });
    }

    function onOfferChange(evt) {
        switch (offerType = evt.target.value) {
            case Manufactured:
            case Workshop | MaterialAndTools:
                mooncakeType = AnyMooncake;
        }
        onFilterChange();
    }

    override function render() {
        var fbMinWidth = 350; // https://developers.facebook.com/docs/plugins/embedded-posts/
        var pagePadding = 15; // bootstrap container-fiuld
        var cardPadding = 15; // static/css/mooncake2020.css .mooncake2020 .card-body
        var gutter = 10;      // static/css/mooncake2020.css
        var safe = 50;        // scrollbar track?
        var breakpoints:DynamicAccess<Int> = {};
        breakpoints["default"] = 4;
        for (numCols in [4, 3, 2]) {
            var minPageWidth = (fbMinWidth + cardPadding * 2) * numCols + pagePadding * 2 + gutter * (numCols - 1) + safe;
            breakpoints[Std.string(minPageWidth)] = numCols - 1;
        }

        function ElevationScroll(props) {
            var trigger = UseScrollTrigger.useScrollTrigger({
                disableHysteresis: true,
                threshold: 0,
            });

            return React.cloneElement(props.children, {
                elevation: trigger ? 4 : 0,
            });
        }

        var filteredData = data
            .filter(d ->
                switch (d.types) {
                    case []:
                        true;
                    case types:
                        types.exists(t -> isMooncakeType(mooncakeType, t));
                }
            )
            .filter(d ->
                switch (offerType) {
                    case Manufactured:
                        !d.note.contains("å·¥ä½œåŠ") && !d.note.contains("ææ–™å·¥å…·");
                    case Workshop:
                        d.note.contains("å·¥ä½œåŠ");
                    case MaterialAndTools:
                        d.note.contains("ææ–™å·¥å…·");
                }
            )
            .map(renderMooncake2020Data);

        var mooncakeTypeControl = switch (offerType) {
            case Workshop | MaterialAndTools:
                null;
            case Manufactured:
                jsx('
                    <FormControl>
                        <InputLabel id="mooncake-type-label">æœˆé¤…æ¬¾å¼</InputLabel>
                        <Select
                            labelId="mooncake-type-label"
                            id="mooncake-type-select"
                            value=${mooncakeType}
                            onChange=${evt -> { mooncakeType = evt.target.value; onFilterChange(); }}
                            disableUnderline=${true}
                            autoWidth=${true}
                        >
                            <MenuItem value=${AnyMooncake}>
                                ${AnyMooncake}
                            </MenuItem>
                            <MenuItem value=${LotusSeedPaste}>
                                ${LotusSeedPaste}
                            </MenuItem>
                            <MenuItem value=${EggCustard}>
                                ${EggCustard}
                            </MenuItem>
                            <MenuItem value=${SnowSkin}>
                                ${SnowSkin}
                            </MenuItem>
                            <MenuItem value=${Nuts}>
                                ${Nuts}
                            </MenuItem>
                            <MenuItem value=${Ham}>
                                ${Ham}
                            </MenuItem>
                            <MenuItem value=${PurpleYam}>
                                ${PurpleYam}
                            </MenuItem>
                            <MenuItem value=${Taro}>
                                ${Taro}
                            </MenuItem>
                            <MenuItem value=${Matcha}>
                                ${Matcha}
                            </MenuItem>
                            <MenuItem value=${Beans}>
                                ${Beans}
                            </MenuItem>
                            <MenuItem value=${Mocha}>
                                ${Mocha}
                            </MenuItem>
                            <MenuItem value=${Pineapple}>
                                ${Pineapple}
                            </MenuItem>
                            <MenuItem value=${Coriander}>
                                ${Coriander}
                            </MenuItem>
                            <MenuItem value=${Vegan}>
                                ${Vegan}
                            </MenuItem>
                            <MenuItem value=${PetFriendly}>
                                ${PetFriendly}
                            </MenuItem>
                        </Select>
                    </FormControl>
                ');
        }

        var rendered = jsx('
            <div>
                <ElevationScroll>
                    <AppBar position="sticky" className="mb-2 bg-light text-body">
                        <Toolbar>
                            <i className="fas fa-filter mr-2"></i>
                            <FormControl>
                                <InputLabel id="offer-type-label">ä¾›æ‡‰</InputLabel>
                                <Select
                                    labelId="offer-type-label"
                                    id="offer-type-select"
                                    value=${offerType}
                                    onChange=${onOfferChange}
                                    disableUnderline=${true}
                                    autoWidth=${true}
                                >
                                    <MenuItem value=${Manufactured}>
                                        ${Manufactured}
                                    </MenuItem>
                                    <MenuItem value=${Workshop}>
                                        ${Workshop}
                                    </MenuItem>
                                    <MenuItem value=${MaterialAndTools}>
                                        ${MaterialAndTools}
                                    </MenuItem>
                                </Select>
                            </FormControl>
                            ${mooncakeTypeControl}
                        </Toolbar>
                    </AppBar>
                </ElevationScroll>
                <div className="container-fluid">
                    <div className="py-2">
                        <Typography noWrap=${true}>
                            ${filteredData.length} é …è³‡æ–™
                        </Typography>
                    </div>
                    <FacebookProvider appId=${Facebook.appId} version=${Facebook.apiVersion}>
                        <Masonry
                            breakpointCols=${breakpoints}
                            className="masonry-grid"
                            columnClassName="masonry-grid-column"
                        >
                            ${filteredData}
                        </Masonry>
                    </FacebookProvider>
                </div>
            </div>
        ');

        if (resized) {
            resized = false;
        }

        return rendered;
    }
}